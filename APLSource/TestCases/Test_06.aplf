 R←Test_06(stopFlag batchFlag);⎕TRAP
⍝ Test "HandleError.Process" with a custom function
 ⎕TRAP←(999 'C' '. ⍝ Deliberate error')(0 'N')
 R←T._Failed

⍝ Preconditions...
 #.⎕SHADOW'⍙HandleErrorParms'
 #.⍙HandleErrorParms←##.HandleError.CreateParms
 #.⍙HandleErrorParms.errorFolder←##.FilesAndDirs.GetTempPath,'HandleError_TestCases\Errors'
 #.⎕SHADOW'TEMP'
 'TEMP'#.⎕NS''
 #.TEMP.TheLog←''
 #.TEMP.⎕FX'{r}←∆CustomFns ps' ' TheLog←ps.∆List'
 #.⍙HandleErrorParms.customFns←'#.TEMP.∆CustomFns'
 {}##.FilesAndDirs.RmDir #.⍙HandleErrorParms.errorFolder
 {}'CREATE!'##.FilesAndDirs.CheckPath #.⍙HandleErrorParms.errorFolder
 ⎕TRAP←(405 'E' '406 ##.HandleError.Process  #.⍙HandleErrorParms')(0 'S')
 :Trap 406
     {⎕SIGNAL ⍵}405
     →T.GoToTidyUp 1
 :EndTrap

 →T.GoToTidyUp 406≠⎕EN
 →T.GoToTidyUp 3≠≢⊃##.FilesAndDirs.Dir #.⍙HandleErrorParms.errorFolder,'\'   ⍝ DCF & HTML & DWS (since acre 4)
 →T.GoToTidyUp(7+1⊃⍴##.HandleError.CreateParms.∆List)≠≢#.TEMP.TheLog
 R←T._OK

∆TidyUp:
 {}##.FilesAndDirs.RmDir #.⍙HandleErrorParms.errorFolder
