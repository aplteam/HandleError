 R←Test_13(stopFlag batchFlag);path;cmd;rc;⎕TRAP;list;home;trap
⍝ Test "SetTrap" with defaults
 ⎕TRAP←(999 'C' '. ⍝ Deliberate error')(0 'N')
 R←T._Failed

 :If batchFlag
     R←T._NoBatchTest ⋄ →0
 :EndIf

⍝ Preconditions...
⍝ Because in this test case we do not specify any parameters we have to mimic what
⍝ `HandleError.Process` is going to do in order to find a proper place for saving stuff.
 :If 'Win'≡APLTreeUtils2.GetOperatingSystem ⍬
     home←FilesAndDirs.NormalizePath'%LOCALAPPDATA%','/',(2⊃⎕NPARTS ⎕WSID),'\Errors'
 :Else
     home←FilesAndDirs.PWD,'/Errors'
 :EndIf


 {}FilesAndDirs.RmDir home
 {}'CREATE!'FilesAndDirs.CheckPath home
 FilesAndDirs.DeleteFile'Tests/CONTINUE.dws'

 ⎕TRAP←1 ##.HandleError.HandleError.SetTrap ⍬
 trap←1⊃⎕TRAP                   ⍝ However, we need to inject...
 (3⊃trap)←'999 ',3⊃trap         ⍝ ... a signal into ⎕Trap ...
 ⎕TRAP←(999 'E' '→⎕LC+1')trap   ⍝ ... for a restart
 {.}&⍬                          ⍝ Causes an error that will be handled by HandleError.Process
 ⎕DL 0.2
 ⎕TRAP←(0 1000)'S'
 →T.GoToTidyUp 0=FilesAndDirs.Exists home
 →T.GoToTidyUp 0=≢list←FilesAndDirs.ListFiles home,'\*'
 →T.GoToTidyUp∨/('-'~⍨10↑APLTreeUtils2.FormatDateTime ⎕TS)∘≢¨{8↑⍵↓⍨⍵⍳'_'}¨{2⊃⎕NPARTS ⍵}¨list

 #.⎕SHADOW'HandleErrorParms'
 #.HandleErrorParms←##.HandleError.HandleError.CreateParms
 #.HandleErrorParms.addToMsg←'Hey there'
 #.HandleErrorParms.createHTML←0
 #.HandleErrorParms.errorFolder←home,'Sub\'
 {}'CREATE!'FilesAndDirs.CheckPath home,'\Sub'
 #.HandleErrorParms.signal←999
 ⎕TRAP←(⊂999 'E' '→∆CarryOn'),1 ##.HandleError.HandleError.SetTrap'#.HandleErrorParms'
 .                              ⍝ Cause a crash
 ⎕TRAP←(999 'C' '. ⍝ Deliberate error')(0 'N')
 →T.GoToTidyUp 1

∆CarryOn:
 ⎕TRAP←(0 1000)'S'
 →T.GoToTidyUp 0=≢FilesAndDirs.ListFiles #.HandleErrorParms.errorFolder

 :Trap 11
     ##.HandleError.HandleError.SetTrap ⎕NS''
     →T.GoToTidyUp 1
 :Else
                                    ⍝ Fine
 :EndTrap

 :Trap 11
     ##.HandleError.HandleError.SetTrap 1 2 3
     →T.GoToTidyUp 1
 :Else
                                    ⍝ Fine
 :EndTrap

 :Trap 11
     ##.HandleError.HandleError.SetTrap'name1' 'name2'
     →T.GoToTidyUp 1
 :Else
                                    ⍝ Fine
 :EndTrap

 R←T._OK

∆TidyUp:
 :If 'Win'≡APLTreeUtils2.GetOperatingSystem ⍬
     {}FilesAndDirs.RmDirByForce 1 FilesAndDirs.NormalizePath home,'/..'
 :Else
     {}FilesAndDirs.RmDirByForce home
 :EndIf
 FilesAndDirs.DeleteFile'Tests/CONTINUE.dws'
