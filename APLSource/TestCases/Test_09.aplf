 R←Test_09(stopFlag batchFlag);⎕TRAP;list;filename;tno;buff
⍝ Test "HandleError.Process": sets "saveErrorWS" to 0
 ⎕TRAP←(999 'C' '. ⍝ Deliberate error')(0 'N')
 R←T._Failed

⍝ Preconditions...
 ⎕TRAP←(405 'E' '406 ##.HandleError.Process  #.HandleErrorParms')(0 'S')
 #.⎕SHADOW'HandleErrorParms'
 #.HandleErrorParms←##.HandleError.CreateParms
 #.HandleErrorParms.saveErrorWS←0
 #.HandleErrorParms.errorFolder←∆GetTempPath⊃⎕SI
 {}##.FilesAndDirs.RmDir #.HandleErrorParms.errorFolder
 {}'CREATE!'##.FilesAndDirs.CheckPath #.HandleErrorParms.errorFolder

 :Trap 406
     {⎕SIGNAL ⍵}405
     →T.GoToTidyUp 1
 :EndTrap

 →T.GoToTidyUp 406≠⎕EN
 →T.GoToTidyUp 2≠⍴⊃##.FilesAndDirs.Dir #.HandleErrorParms.errorFolder,'\'
 →T.GoToTidyUp 1≠≢list←FilesAndDirs.ListFiles #.HandleErrorParms.errorFolder,'/*.dcf'
 filename←⊃list
 tno←filename ⎕FTIE 0
 →T.GoToTidyUp 1≠-/⌽2↑⎕FSIZE tno
 buff←⎕FREAD tno 1
 →T.GoToTidyUp~0<≢buff.⎕NL 2
 →T.GoToTidyUp 9≠buff.⎕NC'Vars'

 R←T._OK

∆TidyUp:
 :Trap 0 ⋄ ⎕FUNTIE tno ⋄ :EndTrap
 {}##.FilesAndDirs.RmDir #.HandleErrorParms.errorFolder
