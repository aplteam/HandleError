 R←Test_02(stopFlag batchFlag);⎕TRAP
⍝ Test "HandleError.Process" with a named (fully qualified!) logging function. See also Test_14.
 ⎕TRAP←(999 'C' '. ⍝ Deliberate error')(0 'N')
 R←T._Failed

⍝ Preconditions...
 #.⎕SHADOW'HandleErrorParms'
 #.HandleErrorParms←##.HandleError.CreateParms
 #.⎕SHADOW'TEMP'
 'TEMP'#.⎕NS''
 #.TEMP.TheLog←''
 #.TEMP.⎕FX'{r}←∆LOG msg' 'TheLog,←⊆msg'
 #.HandleErrorParms.errorFolder←##.FilesAndDirs.GetTempPath,'HandleError_TestCases\Errors'
 {}##.FilesAndDirs.RmDir #.HandleErrorParms.errorFolder
 'Create!'##.FilesAndDirs.CheckPath #.HandleErrorParms.errorFolder
 #.HandleErrorParms.logFunction←'#.TEMP.∆LOG'

 ⎕TRAP←(405 'E' '406 ##.HandleError.Process  #.HandleErrorParms')(0 'S')
 {}##.FilesAndDirs.RmDir #.HandleErrorParms.errorFolder
 {}'CREATE!'##.FilesAndDirs.CheckPath #.HandleErrorParms.errorFolder

 :Trap 406
     {⎕SIGNAL ⍵}405
     →T.GoToTidyUp 406≠⎕EN
     →T.GoToTidyUp 1
 :EndTrap

 →T.GoToTidyUp 406≠⎕EN
 →T.GoToTidyUp 3≠≢⊃##.FilesAndDirs.Dir #.HandleErrorParms.errorFolder,'\'  ⍝ DCF & HTML & DWS (since acre 4)
 →T.GoToTidyUp 5≠≢#.TEMP.TheLog
 →T.GoToTidyUp~∨/'Error number=405'⍷∊#.TEMP.TheLog

 R←T._OK

∆TidyUp:
 {}##.FilesAndDirs.RmDir #.HandleErrorParms.errorFolder,'/..'
