 R←Test_01(stopFlag batchFlag);⎕TRAP;home;ALIEN;parms;test;filename;fno;crash;QEN
⍝ Test "HandleError.Process" without any parameters; just the defaults.
 ⎕TRAP←(999 'C' '. ⍝ Deliberate error')(0 'N')
 R←T._Failed

⍝ Preconditions...
 ⎕TRAP←(405 'E' '406 ##.HandleError.Process ⍬')(0 'S')
 :If 'WIN'≡##.APLTreeUtils2.Uppercase ##.APLTreeUtils2.GetOperatingSystem ⍬
     home←##.FilesAndDirs.NormalizePath'%LOCALAPPDATA%','/',(2⊃⎕NPARTS ⎕WSID),'\Errors'
 :Else
     home←##.FilesAndDirs.PWD,'/Errors'
 :EndIf
 ##.FilesAndDirs.RmDir home
 {}'CREATE!'##.FilesAndDirs.CheckPath home
 ALIEN←'Hello world'

 :Trap 406
     {⎕SIGNAL ⍵}405
     →T.GoToTidyUp 1
 :EndTrap

 QEN←⎕EN  ⍝ Because of stop vectors.
 →T.GoToTidyUp QEN≠406
 →T.GoToTidyUp 3≠≢⊃##.FilesAndDirs.Dir home,'\'    ⍝ DCF & HTML & DWS (since acre 4)
 filename←⊃##.FilesAndDirs.ListFiles home,'\*.dcf'
 fno←filename ⎕FTIE 0
 crash←⎕FREAD fno 1
 ⎕FUNTIE fno
 →T.GoToTidyUp 9≠crash.⎕NC'Vars'
 →T.GoToTidyUp 2≠crash.Vars.⎕NC'ALIEN'
 →T.GoToTidyUp'Hello world'≢crash.Vars.ALIEN

 R←T._OK

∆TidyUp:
 {}##.FilesAndDirs.RmDir home,'/..'
