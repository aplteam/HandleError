 R←Test_16(stopFlag batchFlag);⎕TRAP
⍝ Test "##.HandleError.WriteToLogFile " via a custom log function
 ⎕TRAP←(999 'C' '. ⍝ Deliberate error')(0 'N')
 R←T._Failed

⍝ Preconditions...
 #.⎕SHADOW'TEMP'
 #.HandleErrorParms←##.HandleError.CreateParms
 #.HandleErrorParms.errorFolder←∆GetTempPath⊃⎕SI
 'TEMP'#.⎕NS''
 #.TEMP.∆TheLog←''
 #.TEMP.⎕FX'{r}←∆CustomLogFns msg' '∆TheLog←msg'
 #.HandleErrorParms.logFunction←'∆CustomLogFns'
 #.HandleErrorParms.logFunctionParent←#.TEMP
 #.HandleErrorParms.trapInternalErrors←0
 {}##.FilesAndDirs.RmDir #.HandleErrorParms.errorFolder
 {}'CREATE!'##.FilesAndDirs.CheckPath #.HandleErrorParms.errorFolder
 ⎕TRAP←(405 'E' '406 ##.HandleError.Process  #.HandleErrorParms')(0 'S')
 :Trap 406
     {⎕SIGNAL ⍵}405
     →T.GoToTidyUp 1
 :EndTrap

 →T.GoToTidyUp 406≠⎕EN
 →T.GoToTidyUp 3≠≢⊃##.FilesAndDirs.Dir #.HandleErrorParms.errorFolder,'\'    ⍝ DCF & HTML & DWS (since acre 4)
 →T.GoToTidyUp 3≠1⊃⍴#.TEMP.∆TheLog
 R←T._OK

∆TidyUp:
 {}##.FilesAndDirs.RmDir #.HandleErrorParms.errorFolder
