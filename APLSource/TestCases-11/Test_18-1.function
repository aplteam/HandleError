 R←Test_18(stopFlag batchFlag);⎕TRAP;home;ALIEN;parms;test;filename;fno;crash;fullHome
⍝ Test "HandleError.Process" without saving any variables
 ⎕TRAP←(999 'C' '. ⍝ Deliberate error')(0 'N')
 R←∆Failed

⍝ Preconditions...
 #.⎕SHADOW'parms'
 #.parms←##.HandleError.CreateParms
 #.parms.saveVars←0
 ⎕TRAP←(405 'E' '406 ##.HandleError.Process #.parms')(0 'S')
 home←'Errors'
 :If 'WIN'≡#.APLTreeUtils.Uppercase #.APLTreeUtils.GetOperatingSystem ⍬
     fullHome←#.FilesAndDirs.NormalizePath'%LOCALAPPDATA%','/',(1⊃⎕NPARTS ⎕WSID),'\',home
 :Else
     fullHome←#.FilesAndDirs.PWD,'/Errors'
 :EndIf

 {}#.FilesAndDirs.RmDir fullHome
 ALIEN←'Hello world'

 :Trap 406
     {⎕SIGNAL ⍵}405
     →GoToTidyUp 1
 :EndTrap

 →GoToTidyUp ⎕EN≠406
 ⎕DL 1
 →GoToTidyUp 3≠⍴↑#.FilesAndDirs.Dir fullHome,'\'    ⍝ DCF & HTML & DWS (since acre 4)
 filename←↑#.FilesAndDirs.ListFiles fullHome,'\*.dcf'
 fno←filename ⎕FTIE 0
 crash←⎕FREAD fno 1
 ⎕FUNTIE fno
 →GoToTidyUp 0≠crash.⎕NC'Vars'

 R←∆OK

∆TidyUp:
 {}#.FilesAndDirs.RmDir home
