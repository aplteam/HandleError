 R←Test_06(stopFlag batchFlag);⎕TRAP
⍝ Test "HandleError.Process" with a custom function
 ⎕TRAP←(999 'C' '. ⍝ Deliberate error')(0 'N')
 R←∆Failed

⍝ Preconditions...
 #.⎕SHADOW'⍙HandleErrorParms'
 #.⍙HandleErrorParms←##.HandleError.CreateParms
 #.⍙HandleErrorParms.errorFolder←#.FilesAndDirs.GetTempPath,'HandleError_TestCases\Errors'
 #.⎕SHADOW'TEMP'
 'TEMP'#.⎕NS''
 #.TEMP.TheLog←''
 #.TEMP.⎕FX'{r}←∆CustomFns ps' ' TheLog←ps.∆List'
 #.⍙HandleErrorParms.customFns←'#.TEMP.∆CustomFns'
 {}#.FilesAndDirs.RmDir #.⍙HandleErrorParms.errorFolder
 {}'CREATE!'#.FilesAndDirs.CheckPath #.⍙HandleErrorParms.errorFolder
 ⎕TRAP←(405 'E' '406 ##.HandleError.Process  #.⍙HandleErrorParms')(0 'S')
 :Trap 406
     {⎕SIGNAL ⍵}405
     →GoToTidyUp 1
 :EndTrap

 →GoToTidyUp 406≠⎕EN
 →GoToTidyUp 3≠⍴↑#.FilesAndDirs.Dir #.⍙HandleErrorParms.errorFolder,'\'   ⍝ DCF & HTML & DWS (since acre 4)
 →GoToTidyUp(3+0⊃⍴##.HandleError.CreateParms.∆List)≠0⊃⍴#.TEMP.TheLog
 R←∆OK

∆TidyUp:
 {}#.FilesAndDirs.RmDir #.⍙HandleErrorParms.errorFolder
