 {R}←Test_03(stopFlag batchFlag);myEventLog;buffer;⎕TRAP;QTS
⍝ Test "HandleError.Process": let it write to the Windows Event Log
 ⎕TRAP←(999 'C' '. ⍝ Deliberate error')(0 'N')

 :If 'Win'≢#.APLTreeUtils.GetOperatingSystem ⍬
     R←∆WindowsOnly
     :Return
 :EndIf

 R←∆Failed

⍝ Preconditions...
 ⎕TRAP←(405 'E' '406 ##.HandleError.Process  #.HandleErrorParms')(0 'S')
 myEventLog←⎕NEW #.WindowsEventLog(,⊂'Foo')
 myEventLog.WriteInfo'"Foo" created'
 ⎕EX'myEventLog'

 #.⎕SHADOW'HandleErrorParms'
 #.HandleErrorParms←##.HandleError.CreateParms
 #.HandleErrorParms.addToMsg←QTS←⍕⎕TS
 #.HandleErrorParms.windowsEventSource←'Foo'
 #.HandleErrorParms.errorFolder←#.FilesAndDirs.GetTempPath,'HandleError_TestCases\Errors'
 {}#.FilesAndDirs.RmDir #.HandleErrorParms.errorFolder
 {}'CREATE!'#.FilesAndDirs.CheckPath #.HandleErrorParms.errorFolder
 :Trap 406
     {⎕SIGNAL ⍵}405
     →GoToTidyUp 1
 :EndTrap

 →GoToTidyUp 406≠⎕EN
 →GoToTidyUp 3≠⍴↑#.FilesAndDirs.Dir #.HandleErrorParms.errorFolder,'\'  ⍝ DCF & HTML & DWS (since acre 4)
 myEventLog←⎕NEW #.WindowsEventLog(,⊂'Foo')
 buffer←myEventLog.ReadThese{⎕IO←0 ⋄ (10>⍵):⍳⍵ ⋄ ⍵+-⍳10}myEventLog.NumberOfLogEntries
 →GoToTidyUp 1≠+/∨/¨#.HandleErrorParms.addToMsg∘⍷¨buffer
 R←∆OK

∆TidyUp:
 {}#.FilesAndDirs.RmDir #.HandleErrorParms.errorFolder
